allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: "com.jfrog.artifactory"
    apply plugin: 'maven-publish'

    group 'munch.restful'
    version '3.0.9'

    ext.jUnitVersion = '5.3.2'
    ext.guiceVersion = '4.2.2'
    ext.awsVersion = '1.11.508'

    sourceCompatibility = 1.9
    targetCompatibility = 1.9
    compileJava.options.encoding = 'UTF-8'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact(sourcesJar) {
                    classifier = 'sources'
                }
            }
        }
    }

    configurations.all { resolutionStrategy.cacheChangingModulesFor 0, 'seconds' }

    dependencies {
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnitVersion

        compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    }
}

subprojects {
    task publishLib {
        dependsOn 'build'
        dependsOn 'artifactoryPublish'
        tasks.findByName('artifactoryPublish').mustRunAfter 'build'
    }

    artifactory {
        contextUrl = "${artifactory_contextUrl}"
        publish {
            repository {
                repoKey = "munch-${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release'}"
                username = "${artifactory_user}"
                password = "${artifactory_password}"
                maven = true
            }
            defaults {
                publications('mavenJava')
            }
        }
    }
}

buildscript {
    repositories {
        maven {
            url 'http://fx.artifactory.munchapp.co/artifactory/munch-repos'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.8+"
    }
}

// https://www.jfrog.com/confluence/display/RTF/Gradle+Artifactory+Plugin
artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = "munch-${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release'}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'munch-repos'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}